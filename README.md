# Windows 10 ä¸‹çš„æœ€å° 64 ä½ PE æ–‡ä»¶

[English Version - Minimal PE File on 64-Bit Windows 10](README-en.md)

## ç®€ä»‹

æœ¬æ¬¡å®éªŒå°è¯•åˆ¶ä½œäº† Windows 10 æ“ä½œç³»ç»Ÿä¸‹çš„æœ€å° 64 ä½ PE æ–‡ä»¶ï¼Œè¯¥æ–‡ä»¶å¯ä»¥å¼¹å‡ºå¸¦æœ‰æ–‡å­—æç¤ºçš„æ¶ˆæ¯æ¡†ï¼Œä¸”æ»¡è¶³å®éªŒçš„ä¸‰é¡¹é™åˆ¶æ¡ä»¶ï¼š

- ç¬¦å·é€šè¿‡å‡½æ•°åï¼ˆè€Œä¸æ˜¯å‡½æ•°åºå·ï¼‰å¼•å…¥
- æ¶ˆæ¯æ¡†çš„æ ‡é¢˜å’Œå†…å®¹å­—ç¬¦ä¸²çš„é•¿åº¦ä¸º 64 å­—èŠ‚
- æ€»æ–‡ä»¶å¤§å°ä¸è¶…è¿‡ 300 å­—èŠ‚

ç»è¿‡ä¹ä¸ªæ­¥éª¤åï¼Œæœ€ç»ˆåˆ¶ä½œå‡ºäº† 268 å­—èŠ‚çš„ PE æ–‡ä»¶ã€‚

å®éªŒæ­¥éª¤å¦‚ä¸‹ï¼š

- ç¬¬ä¸€æ­¥ï¼šä½¿ç”¨å¸¸è§„æ–¹æ³•ï¼Œåˆ©ç”¨ C è¯­è¨€ç¼–å†™ç¨‹åºï¼Œä½¿ç”¨ MSVC ç¼–è¯‘å™¨ç”Ÿæˆæ™®é€šçš„ PE æ–‡ä»¶ã€‚
- ç¬¬äºŒæ­¥ï¼šé€šè¿‡æ”¹å˜ MSVC ç¼–è¯‘å™¨ç¼–è¯‘æ—¶çš„é€‰é¡¹ï¼Œå‡å°ç”Ÿæˆçš„ PE æ–‡ä»¶å¤§å°ã€‚
- ç¬¬ä¸‰æ­¥ï¼šä½¿ç”¨ [PE Tools](https://petoolse.github.io/petools/) æŸ¥çœ‹ä¸Šä¸€æ­¥äº§ç”Ÿçš„ PE æ–‡ä»¶çš„å†…éƒ¨ç»“æ„ï¼Œå°†å…¶ä¸­ä¸€äº›æ˜æ˜¾æ— ç”¨çš„éƒ¨åˆ†ç½®é›¶ã€‚æ­¤æ—¶æ–‡ä»¶å¤§å°å¹¶æ²¡æœ‰å‡å°ï¼Œä½†æ–‡ä»¶å†…éƒ¨æœ‰äº†æ›´å¤šçš„é›¶ï¼Œè¿™è®©åç»­æ­¥éª¤æœ‰äº†æ›´å¤šçš„å‹ç¼©ç©ºé—´ã€‚
- ç¬¬å››æ­¥ï¼šåœ¨ PE æ–‡ä»¶çš„å†…éƒ¨ç»“æ„ä¸­ï¼ŒMS-DOS stub ä¹Ÿæ˜¯æ— ç”¨çš„éƒ¨åˆ†ï¼Œæ‰€ä»¥ä½¿ç”¨äºŒè¿›åˆ¶ç¼–è¾‘å™¨æ‰‹åŠ¨å°†è¿™éƒ¨åˆ†å†…å®¹ç½®é›¶ã€‚è¿™ä¸€æ­¥ä¹Ÿç»™äº†åç»­æ­¥éª¤æ›´å¤šçš„å‹ç¼©ç©ºé—´ã€‚
- ç¬¬äº”æ­¥ï¼šè¯¦ç»†åˆ†æ PE æ–‡ä»¶çš„å†…éƒ¨ç»“æ„ï¼Œç†è§£ PE æ–‡ä»¶ä¸­å„éƒ¨åˆ†çš„å«ä¹‰ï¼Œç„¶åä½¿ç”¨æ±‡ç¼–è¯­è¨€æ‰‹åŠ¨ç¼–å†™ä¸€ä¸ªç›¸åŒçš„ PE æ–‡ä»¶ã€‚è™½ç„¶è¿™ä¸€æ­¥ç”Ÿæˆçš„æ–‡ä»¶å’Œä¸Šä¸€æ­¥ç›¸æ¯”æ²¡æœ‰åŒºåˆ«ï¼Œä½†ä½¿ç”¨æ±‡ç¼–è¯­è¨€åï¼Œåç»­æ­¥éª¤å°±æ— éœ€ä¿®æ”¹äºŒè¿›åˆ¶æ–‡ä»¶æœ¬èº«ï¼Œè€Œå¯ä»¥åœ¨æ±‡ç¼–ä»£ç ä¸Šä½œä¿®æ”¹ï¼Œä»è€Œæ›´ç®€ä¾¿åœ°å‡å°æ–‡ä»¶å¤§å°ã€‚
- ç¬¬å…­æ­¥ï¼šæ ¹æ®å¯¹ PE æ–‡ä»¶å†…éƒ¨ç»“æ„çš„åˆ†æï¼Œä»æ±‡ç¼–ä»£ç ä¸Šåˆ é™¤æ‰€æœ‰å¯ä»¥ç›´æ¥åˆ é™¤çš„æ— ç”¨éƒ¨åˆ†ï¼Œä»è€Œå‡å°æ–‡ä»¶å¤§å°ã€‚
- ç¬¬ä¸ƒæ­¥ï¼šè™½ç„¶åœ¨ä¸Šä¸€æ­¥ä¸­åˆ é™¤äº†æ‰€æœ‰å¯ä»¥ç›´æ¥åˆ é™¤çš„æ— ç”¨éƒ¨åˆ†ï¼Œä½†è¿˜æœ‰ä¸€äº›æ— ç”¨å­—æ®µä¸å¯ä»¥è¢«ç›´æ¥åˆ é™¤ã€‚è¿™æ˜¯å› ä¸º PE æ–‡ä»¶å«æœ‰å¤šä¸ªæ–‡ä»¶å¤´ï¼Œè¿™äº›æ— ç”¨å­—æ®µä½äºæ–‡ä»¶å¤´ä¸­ï¼Œè€Œæ–‡ä»¶å¤´çš„æ ¼å¼æ˜¯å›ºå®šçš„ï¼Œä¹Ÿå°±æ˜¯è¯´å³ä½¿æ–‡ä»¶å¤´ä¸­çš„æŸä¸ªå­—æ®µæ²¡æœ‰è¢«ä½¿ç”¨ï¼Œå®ƒä¹Ÿè¦åœ¨æ–‡ä»¶å¤´ä¸­å æ®ç›¸åº”çš„ä½ç½®ï¼Œæ‰€ä»¥ä¸èƒ½ç›´æ¥åˆ é™¤ã€‚ä¸ºæ­¤ï¼Œå¯ä»¥é‡‡å–é‡å çš„æ–¹æ³•ï¼Œé€šè¿‡ç²¾å¿ƒé€‰å–åˆé€‚çš„é‡å æ–¹å¼ï¼Œå°†å¤šä¸ªæ–‡ä»¶å¤´é‡å åœ¨ä¸€èµ·ï¼Œå¹¶ä¿è¯é‡å ä¹‹åï¼Œæ¯ä¸ªé‡å çš„ä½ç½®æœ€å¤šåªèƒ½å¯¹åº”ä¸€ä¸ªæœ‰ç”¨å­—æ®µã€‚è¿™æ ·å°±å¯ä»¥åœ¨ä¸ç ´åæ–‡ä»¶å¤´çš„å‰æä¸‹ï¼Œæœ‰æ•ˆåœ°å‡å°æ–‡ä»¶å¤§å°ã€‚
- ç¬¬å…«æ­¥ï¼šPE æ–‡ä»¶ä¸­è¿˜å«æœ‰äº”æ¡æŒ‡ä»¤çš„æœºå™¨ç ï¼Œå¦‚æœå°†è¿™äº”æ¡æŒ‡ä»¤çš„æœºå™¨ç ä¹ŸæŒ‰ä¸Šä¸€æ­¥çš„æ–¹æ³•ä¸æ–‡ä»¶å¤´é‡å ï¼Œå°±å¯ä»¥è¿›ä¸€æ­¥å‡å°æ–‡ä»¶çš„å¤§å°ã€‚ä½†æ˜¯ï¼ŒæŒ‡ä»¤çš„æœºå™¨ç å¤ªé•¿ï¼Œæ²¡æœ‰åŠæ³•åšåˆ°ã€Œè§ç¼æ’é’ˆã€ï¼Œæ”¾å…¥æ–‡ä»¶å¤´æ— ç”¨å­—æ®µçš„ç©ºéš™ä¸­ã€‚ä¸ºæ­¤ï¼Œæƒ³åˆ°å¯ä»¥å°†äº”æ¡æŒ‡ä»¤æ‹†å¼€ï¼Œå¹¶åœ¨å‰å››æ¡æŒ‡ä»¤çš„åé¢åˆ†åˆ«åŠ ä¸€æ¡çŸ­è·³è½¬æŒ‡ä»¤è·³åˆ°ä¸‹ä¸€æ¡æŒ‡ä»¤çš„ä½ç½®ï¼Œè¿™æ ·æ¯æ¡æŒ‡ä»¤å°±å¯ä»¥ä½œä¸ºä¸€ä¸ªç‹¬ç«‹çš„éƒ¨åˆ†æ’å…¥åˆ°ä¸åŒçš„ç©ºéš™ä¸­äº†ã€‚ç„¶è€Œï¼Œå³ä½¿è¿™æ ·åšä»ç„¶æœ‰ä¸¤æ¡æŒ‡ä»¤å¤ªé•¿ï¼Œæ²¡æœ‰åŠæ³•æ’å…¥ç©ºéš™ã€‚è¿™æ—¶ï¼Œé€šè¿‡å¯¹ x64 æŒ‡ä»¤é›†çš„æ·±å…¥å­¦ä¹ å’Œå……åˆ†æŒæ¡ï¼Œæƒ³åˆ°åœ¨è¿™ä¸¤æ¡æŒ‡ä»¤ä»¥å¦ä¸€ä¸ªå¯„å­˜å™¨ä½œä¸ºåŸºå€æ—¶ï¼Œå¯ä»¥ä½¿å¯¹åº”çš„æœºå™¨ç æ›´çŸ­ï¼Œè€ŒæŒ‡ä»¤ç»“æœä¸å˜ã€‚è¿™æ ·æœºå™¨ç ä¹Ÿæ’å…¥åˆ°äº†æ–‡ä»¶å¤´æ— ç”¨å­—æ®µçš„ç©ºéš™ä¸­ï¼Œä»è€Œè¿›ä¸€æ­¥å‡å°äº†æ–‡ä»¶å¤§å°ã€‚
- ç¬¬ä¹æ­¥ï¼šåˆ é™¤æ–‡ä»¶æœ«å°¾çš„ 0ï¼Œå› ä¸ºç¨‹åºåŠ è½½æ—¶ä¼šåœ¨æœ«å°¾è‡ªåŠ¨å¡«å……é›¶ã€‚

| æ­¥éª¤ | æ–‡ä»¶å¤§å°ï¼ˆå­—èŠ‚ï¼‰ | ç†µ |
| :- | :- | :- |
| 1 | 94208 | 5.924863 |
| 2 | 896 | 3.567715 |
| 3 | 896 | 2.220567 |
| 4 | 896 | 1.608427 |
| 5 | 896 | 1.607880 |
| 6 | 448 | 2.653766 |
| 7 | 308 | 2.473451 |
| 8 | 280 | 2.620173 |
| 9 | 268 | 2.667864 |

åœ¨æœ‰çš„æ­¥éª¤ä¸­ï¼Œè™½ç„¶æ–‡ä»¶å¤§å°æ²¡æœ‰å‡å°ï¼Œä½†æ–‡ä»¶çš„ç†µå‡å°äº†ï¼Œè¿™è¯´æ˜æ–‡ä»¶å†…éƒ¨æœ‰æ›´å¤šçš„é›¶ï¼Œä¹Ÿå°±è¯´æ˜åç»­æ­¥éª¤æœ‰æ›´å¤šçš„å‹ç¼©ç©ºé—´ã€‚

## å®éªŒç¯å¢ƒ

* æ“ä½œç³»ç»Ÿï¼šMicrosoft Windows 10 Version 1903 (v10.0.18362.239) 64-bit
* Shellï¼šzsh 5.7.1 (x86_64-pc-msys)
* C ç¼–è¯‘å™¨ï¼šMicrosoft (R) C/C++ Optimizing Compiler Version 19.22.27905 for x64
* è¿æ¥å™¨ï¼šMicrosoft (R) Incremental Linker Version 14.22.27905.0
* æ±‡ç¼–å™¨ï¼šNASM version 2.14.02 compiled on Jan 30 2019
* è°ƒè¯•å™¨ï¼šx64dbg May 19 2019, 18:13:13

## å®éªŒæ­¥éª¤

**ç¬¬ä¸€æ­¥ï¼šä½¿ç”¨å¸¸è§„æ–¹æ³•ï¼Œåˆ©ç”¨ C è¯­è¨€ç¼–å†™ç¨‹åºï¼Œä½¿ç”¨ MSVC ç¼–è¯‘å™¨ç”Ÿæˆæ™®é€šçš„ PE æ–‡ä»¶ã€‚**

åœ¨ Windows ç¼–ç¨‹ä¸­ï¼Œè¦å¼¹å‡ºæ¶ˆæ¯æ¡†ï¼Œå¯ä»¥ä½¿ç”¨ [`MessageBoxA`](https://docs.microsoft.com/en-us/windows/desktop/api/winuser/nf-winuser-messageboxa) æˆ– [`MessageBoxW`](https://docs.microsoft.com/en-us/windows/win32/api/winuser/nf-winuser-messageboxw) è¿™ä¸¤ä¸ªå‡½æ•°ã€‚å…¶ä¸­ï¼Œå‡½æ•°åä»¥ A ç»“å°¾è¡¨ç¤ºå­—ç¬¦ç¼–ç ä½¿ç”¨ç”¨æˆ·çš„å½“å‰ä»£ç é¡µï¼Œä»¥ W ç»“å°¾è¡¨ç¤ºä½¿ç”¨ UTF-16ã€‚

ç”±äºç”¨æˆ·ä½¿ç”¨çš„ä»£ç é¡µå¹¶ä¸ç»Ÿä¸€ï¼Œå‡ºäºå…¼å®¹æ€§çš„è€ƒè™‘ï¼Œæœ¬æ¬¡å®éªŒä½¿ç”¨ä»¥ W ç»“å°¾çš„ `MessageBoxW` å‡½æ•°ã€‚ä»£ç é¡µçš„é—®é¢˜å…¶å®éå¸¸å¸¸è§ï¼Œç›¸ä¿¡è®¸å¤šäººåœ¨ Windows ä¸Šä½¿ç”¨ Python ç¼–ç¨‹æ—¶ï¼Œéƒ½é‡åˆ°è¿‡å› ä¸ºä»£ç é¡µä¸å¯¹è€Œå¯¼è‡´ç¨‹åºä¹±ç æˆ–å´©æºƒçš„é—®é¢˜ã€‚è¿™é‡Œä½¿ç”¨ `MessageBoxW` å‡½æ•°ï¼Œä¸ä»£ç é¡µæ— å…³ï¼Œæ‰€ä»¥å¯ä»¥é¿å…ç¨‹åºä¹±ç æˆ–å´©æºƒã€‚

ç¼–å†™çš„ C è¯­è¨€ä»£ç  `tiny.c` å¦‚ä¸‹ï¼š

```c
#include <Windows.h>

int main() {
    MessageBoxW( NULL /* hWnd */,
                 L"ABCDEFG" /* lpText, 16 bytes */,
                 L"ğŸ’¯ TinyPE on Windows 10" /* lpCaption, 48 bytes */,
                 MB_ICONASTERISK | MB_TOPMOST | MB_SERVICE_NOTIFICATION /* uType */ );
    return 0;
}
```

æ¥ä¸‹æ¥å°† C è¯­è¨€ä»£ç ç¼–è¯‘ä¸º PE æ–‡ä»¶ã€‚

å®‰è£… Visual Studio Build Tools 2019 åï¼Œã€Œå¼€å§‹ã€èœå•ä¸­ä¼šå‡ºç° Visual Studio 2019 æ–‡ä»¶å¤¹ã€‚ç‚¹å‡»å…¶ä¸­çš„ x64 Native Tools Command Prompt for VS 2019 æ‰“å¼€å‘½ä»¤è¡Œï¼Œåˆ‡æ¢åˆ°å½“å‰ç›®å½•åï¼Œè¾“å…¥ä»¥ä¸‹å‘½ä»¤ï¼š

```cmd
> cl /O1 /source-charset:utf-8 tiny.c /link /SUBSYSTEM:WINDOWS /ENTRY:mainCRTStartup user32.lib
Microsoft (R) C/C++ Optimizing Compiler Version 19.22.27905 for x64
Copyright (C) Microsoft Corporation.  All rights reserved.

tiny.c
Microsoft (R) Incremental Linker Version 14.22.27905.0
Copyright (C) Microsoft Corporation.  All rights reserved.

/out:tiny.exe
/SUBSYSTEM:WINDOWS
/ENTRY:mainCRTStartup
user32.lib
tiny.obj
```

ç¼–è¯‘ç”Ÿæˆ `tiny.exe` æ–‡ä»¶ï¼Œå¯ä»¥æ­£å¸¸è¿è¡Œï¼š

![](images/tiny1.exe.png)

**ç¬¬äºŒæ­¥ï¼šé€šè¿‡æ”¹å˜ MSVC ç¼–è¯‘å™¨ç¼–è¯‘æ—¶çš„é€‰é¡¹ï¼Œå‡å°ç”Ÿæˆçš„ PE æ–‡ä»¶å¤§å°ã€‚**

å‚è€ƒ [Minimize the size of your program â€“ high level](https://blogs.msdn.microsoft.com/xiangfan/2008/09/19/minimize-the-size-of-your-program-high-level/) å’Œ [Linker Options](https://docs.microsoft.com/en-us/cpp/build/reference/linker-options?view=vs-2019) å¯çŸ¥ï¼Œé€šè¿‡ä¿®æ”¹ C è¯­è¨€ä»£ç å’Œç¼–è¯‘é€‰é¡¹ï¼Œå¯ä»¥å‡å°ç¼–è¯‘ç”Ÿæˆçš„ PE æ–‡ä»¶å¤§å°ã€‚

ä¿®æ”¹åçš„ C è¯­è¨€ä»£ç  `tiny.c` å¦‚ä¸‹ï¼š

```c
#include <Windows.h>

void _() {
    MessageBoxW( NULL /* hWnd */,
                 L"ABCDEFG" /* lpText, 16 bytes */,
                 L"ğŸ’¯ TinyPE on Windows 10" /* lpCaption, 48 bytes */,
                 MB_ICONASTERISK | MB_TOPMOST | MB_SERVICE_NOTIFICATION /* uType */ );
}
```

ä½¿ç”¨ä»¥ä¸‹å‘½ä»¤è¡Œé€‰é¡¹ç¼–è¯‘ï¼š

```cmd
> cl /O1 /MD /GS- /source-charset:utf-8 tiny.c /link /NOLOGO /NODEFAULTLIB /SUBSYSTEM:WINDOWS /ENTRY:_ /MERGE:.rdata=. /MERGE:.pdata=. /MERGE:.text=. /SECTION:.,ER /ALIGN:16 user32.lib
Microsoft (R) C/C++ Optimizing Compiler Version 19.22.27905 for x64
Copyright (C) Microsoft Corporation.  All rights reserved.

tiny.c
LINK : warning LNK4108: /ALIGN specified without /DRIVER; image may not run
LINK : warning LNK4254: section '.text' (60000020) merged into '.' (40000040) with different attributes
```

ç¼–è¯‘ç”Ÿæˆ `tiny.exe` æ–‡ä»¶ï¼Œå¯ä»¥æ­£å¸¸è¿è¡Œã€‚

**ç¬¬ä¸‰æ­¥ï¼šä½¿ç”¨ [PE Tools](https://petoolse.github.io/petools/) æŸ¥çœ‹ä¸Šä¸€æ­¥äº§ç”Ÿçš„ PE æ–‡ä»¶çš„å†…éƒ¨ç»“æ„ï¼Œå°†å…¶ä¸­ä¸€äº›æ˜æ˜¾æ— ç”¨çš„éƒ¨åˆ†ç½®é›¶ã€‚æ­¤æ—¶æ–‡ä»¶å¤§å°å¹¶æ²¡æœ‰å‡å°ï¼Œä½†æ–‡ä»¶å†…éƒ¨æœ‰äº†æ›´å¤šçš„é›¶ï¼Œè¿™è®©åç»­æ­¥éª¤æœ‰äº†æ›´å¤šçš„å‹ç¼©ç©ºé—´ã€‚**

æ‰“å¼€ PE Tools (v1.9.762.2018)ï¼Œå•å‡»â€œPE Editorâ€ï¼Œç„¶åæ‰“å¼€ `tiny.exe`ã€‚

![](images/tiny2.exe.png)

æ‰¾åˆ°ä»¥ä¸‹ä¸‰ä¸ªéƒ¨åˆ†ï¼Œå¹¶ç½®é›¶ï¼š

* ç‚¹å‡»â€œFile Headerâ€ï¼Œæ‰¾åˆ°â€œTime/Dateâ€ï¼Œè®¾ç½®ä¸º 0
* ç‚¹å‡»â€œView Richâ€ï¼Œç„¶åç‚¹å‡»â€œClear Signâ€
* ç‚¹å‡»â€œDirectoriesâ€ï¼Œæ‰¾åˆ°â€œDebug Directoryâ€ï¼Œç‚¹å‡»â€œ...â€ï¼Œç„¶åç‚¹å‡»â€œClear debug infoâ€

ä¿®æ”¹åçš„ `tiny.exe` æ–‡ä»¶å¯ä»¥æ­£å¸¸è¿è¡Œã€‚

**ç¬¬å››æ­¥ï¼šåœ¨ PE æ–‡ä»¶çš„å†…éƒ¨ç»“æ„ä¸­ï¼ŒMS-DOS stub ä¹Ÿæ˜¯æ— ç”¨çš„éƒ¨åˆ†ï¼Œæ‰€ä»¥ä½¿ç”¨äºŒè¿›åˆ¶ç¼–è¾‘å™¨æ‰‹åŠ¨å°†è¿™éƒ¨åˆ†å†…å®¹ç½®é›¶ã€‚è¿™ä¸€æ­¥ä¹Ÿç»™äº†åç»­æ­¥éª¤æ›´å¤šçš„å‹ç¼©ç©ºé—´ã€‚**

ä½¿ç”¨äºŒè¿›åˆ¶ç¼–è¾‘å™¨æ‰“å¼€ `tiny.exe`ã€‚

å®šä½åˆ°ä»¥ä¸‹éƒ¨åˆ†ï¼Œå¹¶ç½®é›¶ï¼š

- å°† `0x02-0x3b` ç½®é›¶
- å°† `0x40-0x7f` ç½®é›¶

è¿™é‡Œéœ€è¦æ³¨æ„ï¼Œ`0x00-0x01` æ˜¯ `e_magic`ï¼Œ`0x3c-0x3f` æ˜¯ `e_lfanew`ï¼Œå®ƒä»¬éƒ½æ˜¯æœ‰ç”¨çš„å­—æ®µï¼Œæ‰€ä»¥æ²¡æœ‰ç½®é›¶ã€‚

ä¿®æ”¹åçš„ `tiny.exe` æ–‡ä»¶å¯ä»¥æ­£å¸¸è¿è¡Œï¼Œå†…å®¹å¦‚ä¸‹ï¼š

```zsh
$ xxd -p tiny.exe
4d5a00000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
c00000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000504500006486010000000000000000000000
0000f00022000b020e1690010000000000000000000000030000f0010000
000000400100000010000000100000000600000000000000060000000000
000080030000f00100000000000002006081000010000000000000100000
000000000000100000000000001000000000000000000000100000000000
000000000000200300002800000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
00000000f001000010000000000000000000000000000000000000000000
0000000000002e0000000000000082010000f001000090010000f0010000
000000000000000000000000200000605803000000000000000000000000
00003dd8afdc2000540069006e0079005000450020006f006e0020005700
69006e0064006f0077007300200031003000000041004200430044004500
460047000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
000000000000000000000000000000000000000000000000000000000000
00000000000000000000000000000000000041b9400024004c8d05f3feff
ff488d151cffffff33c948ff25d3feffffcccccc48030000000000000000
000066030000f00100000000000000000000000000000000000000000000
5803000000000000000000000000000094024d657373616765426f785700
5553455233322e646c6c00000000000000000000000000000000
```

å¯ä»¥ä½¿ç”¨ `xxd -p -r` å°†ä»¥ä¸Šæ–‡æœ¬è½¬æ¢å›äºŒè¿›åˆ¶æ–‡ä»¶ã€‚

**ç¬¬äº”æ­¥ï¼šè¯¦ç»†åˆ†æ PE æ–‡ä»¶çš„å†…éƒ¨ç»“æ„ï¼Œç†è§£ PE æ–‡ä»¶ä¸­å„éƒ¨åˆ†çš„å«ä¹‰ï¼Œç„¶åä½¿ç”¨æ±‡ç¼–è¯­è¨€æ‰‹åŠ¨ç¼–å†™ä¸€ä¸ªç›¸åŒçš„ PE æ–‡ä»¶ã€‚è™½ç„¶è¿™ä¸€æ­¥ç”Ÿæˆçš„æ–‡ä»¶å’Œä¸Šä¸€æ­¥ç›¸æ¯”æ²¡æœ‰åŒºåˆ«ï¼Œä½†ä½¿ç”¨æ±‡ç¼–è¯­è¨€åï¼Œåç»­æ­¥éª¤å°±æ— éœ€ä¿®æ”¹äºŒè¿›åˆ¶æ–‡ä»¶æœ¬èº«ï¼Œè€Œå¯ä»¥åœ¨æ±‡ç¼–ä»£ç ä¸Šä½œä¿®æ”¹ï¼Œä»è€Œæ›´ç®€ä¾¿åœ°å‡å°æ–‡ä»¶å¤§å°ã€‚**

åœ¨ç†è§£ PE æ–‡ä»¶çš„ç»“æ„æ—¶ï¼Œä¸»è¦å‚è€ƒäº†ä»¥ä¸‹èµ„æ–™ï¼š

- æ€»è¿°ï¼š[PE Format](https://docs.microsoft.com/en-us/windows/desktop/Debug/pe-format)ï¼Œ[x86 Disassembly/Windows Executable Files](https://en.wikibooks.org/wiki/X86_Disassembly/Windows_Executable_Files)ï¼Œ[Portable Executable File Format](https://blog.kowalczyk.info/articles/pefileformat.html)
- Rich headerï¼š[The Undocumented Microsoft "Rich" Header](http://bytepointer.com/articles/the_microsoft_rich_header.htm)
- Import address tableï¼š[Import table vs Import Address Table](https://reverseengineering.stackexchange.com/a/16872)

å¦å¤–ï¼Œåœ¨ç†è§£ PE æ–‡ä»¶çš„ç»“æ„æ—¶ï¼Œè¿˜ä½¿ç”¨äº† [PE Tools](https://petoolse.github.io/petools/) å’Œ [PE Disassembler viewer](http://www.codedebug.com/php/Products/Products_NikPEViewer_20v.php) è¿™ä¸¤ä¸ªå·¥å…·ã€‚

åœ¨ç†è§£ PE æ–‡ä»¶çš„ç»“æ„åï¼Œä½¿ç”¨æ±‡ç¼–è¯­è¨€æ‰‹åŠ¨ç¼–å†™ä¸€ä¸ªç›¸åŒçš„ PE æ–‡ä»¶ã€‚

æ³¨æ„åœ¨ä½¿ç”¨ NASM æ±‡ç¼–å™¨æ—¶ï¼Œæ ¹æ®[å®˜æ–¹æ–‡æ¡£](https://www.nasm.us/doc/nasmdoc3.html#section-3.2.1)ï¼Œåˆ†åˆ«ä½¿ç”¨ä¼ªæŒ‡ä»¤ `db`ã€`dw`ã€`dd`ã€`dq` å£°æ˜ 1ã€2ã€4ã€8 å­—èŠ‚çš„æ•°æ®ã€‚

æ­¤å¤–ï¼Œåœ¨ç¼–å†™æ±‡ç¼–è¯­è¨€æ—¶ï¼Œè¦ç‰¹åˆ«æ³¨æ„ä¸èƒ½ä½¿ç”¨ç¡¬ç¼–ç çš„æ•°å€¼ï¼Œè€Œæ˜¯è¦ä½¿ç”¨ä¼ªæŒ‡ä»¤è®¡ç®—å¾—å‡ºç›¸åº”çš„æ•°å€¼ã€‚ä¾‹å¦‚ï¼Œæ–‡ä»¶å¤§å°çš„æ•°å€¼ `0x0380` ä½¿ç”¨ `file_size equ $-$$` æ›¿æ¢ï¼Œæœºå™¨æŒ‡ä»¤ `0x41b940002400` ä½¿ç”¨ `mov r9d, 0x00240040` æ›¿æ¢ã€‚è¿™æ˜¯å› ä¸ºå¦‚æœä½¿ç”¨äº†ç¡¬ç¼–ç çš„æ•°å€¼ï¼Œåç»­æ­¥éª¤ä¸­ PE æ–‡ä»¶çš„ç»“æ„å‘ç”Ÿå˜åŒ–æ—¶ï¼Œè¿™äº›æ•°å€¼å¹¶ä¸ä¼šéšä¹‹æ”¹å˜ï¼Œæ–‡ä»¶å°±ä¼šæŸåã€‚

ç¼–å†™çš„æ±‡ç¼–è¯­è¨€æ–‡ä»¶ `stretch.asm` å¦‚ä¸‹ï¼š

```assembly
BITS 64

%define align(n,r) (((n+(r-1))/r)*r)

; DOS Header
    dw 'MZ'                 ; e_magic
    dw 0                    ; [UNUSED] e_cblp
    dw 0                    ; [UNUSED] c_cp
    dw 0                    ; [UNUSED] e_crlc
    dw 0                    ; [UNUSED] e_cparhdr
    dw 0                    ; [UNUSED] e_minalloc
    dw 0                    ; [UNUSED] e_maxalloc
    dw 0                    ; [UNUSED] e_ss
    dw 0                    ; [UNUSED] e_sp
    dw 0                    ; [UNUSED] e_csum
    dw 0                    ; [UNUSED] e_ip
    dw 0                    ; [UNUSED] e_cs
    dw 0                    ; [UNUSED] e_lfarlc
    dw 0                    ; [UNUSED] e_ovno
    times 4 dw 0            ; [UNUSED] e_res
    dw 0                    ; [UNUSED] e_oemid
    dw 0                    ; [UNUSED] e_oeminfo
    times 10 dw 0           ; [UNUSED] e_res2
    dd pe_hdr               ; e_lfanew

; DOS Stub
    times 8 dq 0            ; [UNUSED] DOS Stub

; Rich Header
    times 8 dq 0            ; [UNUSED] Rich Header

; PE Header
pe_hdr:
    dw 'PE', 0              ; Signature

; Image File Header
    dw 0x8664               ; Machine
    dw 0x01                 ; NumberOfSections
    dd 0                    ; [UNUSED] TimeDateStamp
    dd 0                    ; PointerToSymbolTable
    dd 0                    ; NumberOfSymbols
    dw opt_hdr_size         ; SizeOfOptionalHeader
    dw 0x22                 ; Characteristics

; Optional Header, COFF Standard Fields
opt_hdr:
    dw 0x020b               ; Magic (PE32+)
    db 0x0e                 ; MajorLinkerVersion
    db 0x16                 ; MinorLinkerVersion
    dd code_size            ; SizeOfCode
    dd 0                    ; SizeOfInitializedData
    dd 0                    ; SizeOfUninitializedData
    dd entry                ; AddressOfEntryPoint
    dd iatbl                ; BaseOfCode

; Optional Header, NT Additional Fields
    dq 0x000140000000       ; ImageBase
    dd 0x10                 ; SectionAlignment
    dd 0x10                 ; FileAlignment
    dw 0x06                 ; MajorOperatingSystemVersion
    dw 0                    ; MinorOperatingSystemVersion
    dw 0                    ; MajorImageVersion
    dw 0                    ; MinorImageVersion
    dw 0x06                 ; MajorSubsystemVersion
    dw 0                    ; MinorSubsystemVersion
    dd 0                    ; Reserved1
    dd file_size            ; SizeOfImage
    dd hdr_size             ; SizeOfHeaders
    dd 0                    ; CheckSum
    dw 0x02                 ; Subsystem (Windows GUI)
    dw 0x8160               ; DllCharacteristics
    dq 0x100000             ; SizeOfStackReserve
    dq 0x1000               ; SizeOfStackCommit
    dq 0x100000             ; SizeOfHeapReserve
    dq 0x1000               ; SizeOfHeapCommit
    dd 0                    ; LoaderFlags
    dd 0x10                 ; NumberOfRvaAndSizes

; Optional Header, Data Directories
    dd 0                    ; Export, RVA
    dd 0                    ; Export, Size
    dd itbl                 ; Import, RVA
    dd itbl_size            ; Import, Size
    dd 0                    ; Resource, RVA
    dd 0                    ; Resource, Size
    dd 0                    ; Exception, RVA
    dd 0                    ; Exception, Size
    dd 0                    ; Certificate, RVA
    dd 0                    ; Certificate, Size
    dd 0                    ; Base Relocation, RVA
    dd 0                    ; Base Relocation, Size
    dd 0                    ; Debug, RVA
    dd 0                    ; Debug, Size
    dd 0                    ; Architecture, RVA
    dd 0                    ; Architecture, Size
    dd 0                    ; Global Ptr, RVA
    dd 0                    ; Global Ptr, Size
    dd 0                    ; TLS, RVA
    dd 0                    ; TLS, Size
    dd 0                    ; Load Config, RVA
    dd 0                    ; Load Config, Size
    dd 0                    ; Bound Import, RVA
    dd 0                    ; Bound Import, Size
    dd iatbl                ; IAT, RVA
    dd iatbl_size           ; IAT, Size
    dd 0                    ; Delay Import Descriptor, RVA
    dd 0                    ; Delay Import Descriptor, Size
    dd 0                    ; CLR Runtime Header, RVA
    dd 0                    ; CLR Runtime Header, Size
    dd 0                    ; Reserved, RVA
    dd 0                    ; Reserved, Size

opt_hdr_size equ $-opt_hdr

; Section Table
    section_name db '.'     ; Name
    times 8-($-section_name) db 0
    dd sect_size            ; VirtualSize
    dd iatbl                ; VirtualAddress
    dd code_size            ; SizeOfRawData
    dd iatbl                ; PointerToRawData
    dd 0                    ; PointerToRelocations
    dd 0                    ; PointerToLinenumbers
    dw 0                    ; NumberOfRelocations
    dw 0                    ; NumberOfLinenumbers
    dd 0x60000020           ; Characteristics

hdr_size equ $-$$

code:
; Import Address Directory
iatbl:
    dq symbol
    dq 0

iatbl_size equ $-iatbl

; Strings
title:
    db 0x3d,0xd8,0xaf,0xdc,0x20,0x00,0x54,0x00
    db 0x69,0x00,0x6e,0x00,0x79,0x00,0x50,0x00
    db 0x45,0x00,0x20,0x00,0x6f,0x00,0x6e,0x00
    db 0x20,0x00,0x57,0x00,0x69,0x00,0x6e,0x00
    db 0x64,0x00,0x6f,0x00,0x77,0x00,0x73,0x00
    db 0x20,0x00,0x31,0x00,0x30,0x00,0,0
content:
    db 0x41,0x00,0x42,0x00,0x43,0x00,0x44,0x00
    db 0x45,0x00,0x46,0x00,0x47,0x00,0,0

; Debug Table
    times 24 dq 0           ; [UNUSED] Debug Table

; Entry
entry:
    mov r9d, 0x00240040     ; uType
    lea r8, [rel title]     ; lpCaption
    lea rdx, [rel content]  ; lpText
    xor ecx, ecx            ; hWnd
    jmp [rel iatbl]         ; MessageBoxW

    times align($-$$,16)-($-$$) db 0xcc

; Import Directory
itbl:
    dq intbl                ; OriginalFirstThunk
    dd 0                    ; TimeDateStamp
    dd dll_name             ; ForwarderChain
    dd iatbl                ; Name
    dq 0                    ; FirstThunk

    times 3 dd 0

itbl_size equ $-itbl

; Import Name Table
intbl:
    dq symbol
    dq 0

; Symbol
symbol:
    dw 0x0294               ; [UNUSED] Function Order
    db 'MessageBoxW', 0     ; Function Name
dll_name:
    db 'USER32.dll', 0
    db 0

sect_size equ $-code

    times align($-$$,16)-($-$$) db 0

code_size equ $-code
file_size equ $-$$
```

ç¼–è¯‘ï¼š

```zsh
$ nasm -f bin -o stretch.exe -l stretch.lst stretch.asm
```

ç¼–è¯‘ç”Ÿæˆ `stretch.exe`ï¼Œå¯ä»¥æ­£å¸¸è¿è¡Œã€‚

è¿™æ—¶å‘ç°ï¼Œè™½ç„¶ `stretch.exe` æ˜¯å‚ç…§ `tiny.exe` æ‰‹åŠ¨ç¼–å†™çš„ï¼Œç†è®ºä¸Šåº”è¯¥å®Œå…¨ç›¸åŒï¼Œä½†æ˜¯å®é™…ä¸Šä¸¤è€…ç•¥æœ‰åŒºåˆ«ï¼š

```zsh
$ diff =(xxd step4/tiny.exe) =(xxd step5/stretch.exe)
50c50
< 00000310: 1cff ffff 33c9 48ff 25d3 feff ffcc cccc  ....3.H.%.......
---
> 00000310: 1cff ffff 31c9 ff25 d4fe ffff cccc cccc  ....1..%........
```

`xor ecx, ecx` æŒ‡ä»¤åœ¨ `tiny.exe` ä¸­ä»¥æœºå™¨ç  `0x33c9` è¡¨ç¤ºï¼Œè€Œåœ¨ `stretch.exe` ä¸­ä»¥ `0x31c9` è¡¨ç¤ºã€‚ç”± [XOR â€” Logical Exclusive OR](https://www.felixcloutier.com/x86/xor) å¯çŸ¥ï¼Œè¿™æ˜¯ç”±äºæŒ‡ä»¤ç¼–ç æ–¹å¼ä¸åŒï¼Œä¸å½±å“æŒ‡ä»¤çš„æ•ˆæœï¼š

```zsh
$ echo 33c9 | xxd -p -r - | ndisasm -b 64 -
00000000  33C9              xor ecx,ecx
$ echo 31c9 | xxd -p -r - | ndisasm -b 64 -
00000000  31C9              xor ecx,ecx
```

`jmp [rel iatbl]` æŒ‡ä»¤åœ¨ `tiny.exe` ä¸­ä»¥æœºå™¨ç  `0x48ff25d3feffff` è¡¨ç¤ºï¼Œè€Œåœ¨ `stretch.exe` ä¸­ä»¥ `0xff25d4feffff` è¡¨ç¤ºã€‚è¿™ä¸¤æ¡æŒ‡ä»¤çš„è·³è½¬åœ°å€æ²¡æœ‰åŒºåˆ«ï¼š

```zsh
$ echo 48ff25d3feffff | xxd -p -r - | ndisasm -b 64 -
00000000  48FF25D3FEFFFF    jmp qword [rel 0xfffffffffffffeda]
$ echo ff25d4feffff | xxd -p -r - | ndisasm -b 64 -
00000000  FF25D4FEFFFF      jmp [rel 0xfffffffffffffeda]
```

ç”± Stack Overflow ä¸Šçš„[ä¸€ä¸ªå›ç­”](https://stackoverflow.com/a/36800114) å¯çŸ¥ï¼Œæœºå™¨ç ä¸­çš„ `48` å‰ç¼€è¡¨ç¤º REX.Wï¼Œä¼šè¢«å¤„ç†å™¨å¿½ç•¥ã€‚è¿™ä¸€å‰ç¼€å¯èƒ½ä¸ Windows x64 çš„ unwind data æœ‰å…³ã€‚ä¸è®ºå¦‚ä½•ï¼Œä»è¿è¡Œç»“æœä¸Šçœ‹ï¼Œè¿™ä¸€ä¿®æ”¹ä¸å½±å“æŒ‡ä»¤çš„æ•ˆæœã€‚

ç”±æ­¤å¯çŸ¥ï¼Œè¿™ä¸€æ­¥ä½¿ç”¨æ±‡ç¼–è¯­è¨€ç¼–å†™çš„ `stretch.exe` å’Œä¸Šä¸€æ­¥çš„ `tiny.exe` æ˜¯ç­‰ä»·çš„ã€‚

**ç¬¬å…­æ­¥ï¼šæ ¹æ®å¯¹ PE æ–‡ä»¶å†…éƒ¨ç»“æ„çš„åˆ†æï¼Œä»æ±‡ç¼–ä»£ç ä¸Šåˆ é™¤æ‰€æœ‰å¯ä»¥ç›´æ¥åˆ é™¤çš„æ— ç”¨éƒ¨åˆ†ï¼Œä»è€Œå‡å°æ–‡ä»¶å¤§å°ã€‚**

åˆ é™¤çš„éƒ¨åˆ†å¦‚ä¸‹ï¼š

- åˆ é™¤ DOS stubã€Rich header å’Œ debug table
- å¯¹äº optional header çš„ data directories ä¸­çš„ 16 é¡¹ï¼Œä»…ä¿ç•™å‰ 2 é¡¹ï¼Œå¹¶å°† `NumberOfRvaAndSizes` è®¾ç½®ä¸º 2
- åˆ é™¤ `itbl` åç”¨äºå¯¹é½çš„å­—èŠ‚

ä¿®æ”¹åçš„ `stretch.asm` å¦‚ä¸‹ï¼š

```zsh
$ diff step5/stretch.asm step6/stretch.asm
26,31d25
< ; DOS Stub
<     times 8 dq 0            ; [UNUSED] DOS Stub
< 
< ; Rich Header
<     times 8 dq 0            ; [UNUSED] Rich Header
< 
77c71
<     dd 0x10                 ; NumberOfRvaAndSizes
---
>     dd 0x02                 ; NumberOfRvaAndSizes
84,111d77
<     dd 0                    ; Resource, RVA
<     dd 0                    ; Resource, Size
<     dd 0                    ; Exception, RVA
<     dd 0                    ; Exception, Size
<     dd 0                    ; Certificate, RVA
<     dd 0                    ; Certificate, Size
<     dd 0                    ; Base Relocation, RVA
<     dd 0                    ; Base Relocation, Size
<     dd 0                    ; Debug, RVA
<     dd 0                    ; Debug, Size
<     dd 0                    ; Architecture, RVA
<     dd 0                    ; Architecture, Size
<     dd 0                    ; Global Ptr, RVA
<     dd 0                    ; Global Ptr, Size
<     dd 0                    ; TLS, RVA
<     dd 0                    ; TLS, Size
<     dd 0                    ; Load Config, RVA
<     dd 0                    ; Load Config, Size
<     dd 0                    ; Bound Import, RVA
<     dd 0                    ; Bound Import, Size
<     dd iatbl                ; IAT, RVA
<     dd iatbl_size           ; IAT, Size
<     dd 0                    ; Delay Import Descriptor, RVA
<     dd 0                    ; Delay Import Descriptor, Size
<     dd 0                    ; CLR Runtime Header, RVA
<     dd 0                    ; CLR Runtime Header, Size
<     dd 0                    ; Reserved, RVA
<     dd 0                    ; Reserved, Size
150,152d115
< ; Debug Table
<     times 24 dq 0           ; [UNUSED] Debug Table
< 
170,171d132
< 
<     times 3 dd 0
```

ç¼–è¯‘ï¼š

```zsh
$ nasm -f bin -o stretch.exe -l stretch.lst stretch.asm
```

ç¼–è¯‘ç”Ÿæˆ `stretch.exe`ï¼Œå¯ä»¥æ­£å¸¸è¿è¡Œã€‚

**ç¬¬ä¸ƒæ­¥ï¼šè™½ç„¶åœ¨ä¸Šä¸€æ­¥ä¸­åˆ é™¤äº†æ‰€æœ‰å¯ä»¥ç›´æ¥åˆ é™¤çš„æ— ç”¨éƒ¨åˆ†ï¼Œä½†è¿˜æœ‰ä¸€äº›æ— ç”¨å­—æ®µä¸å¯ä»¥è¢«ç›´æ¥åˆ é™¤ã€‚è¿™æ˜¯å› ä¸º PE æ–‡ä»¶å«æœ‰å¤šä¸ªæ–‡ä»¶å¤´ï¼Œè¿™äº›æ— ç”¨å­—æ®µä½äºæ–‡ä»¶å¤´ä¸­ï¼Œè€Œæ–‡ä»¶å¤´çš„æ ¼å¼æ˜¯å›ºå®šçš„ï¼Œä¹Ÿå°±æ˜¯è¯´å³ä½¿æ–‡ä»¶å¤´ä¸­çš„æŸä¸ªå­—æ®µæ²¡æœ‰è¢«ä½¿ç”¨ï¼Œå®ƒä¹Ÿè¦åœ¨æ–‡ä»¶å¤´ä¸­å æ®ç›¸åº”çš„ä½ç½®ï¼Œæ‰€ä»¥ä¸èƒ½ç›´æ¥åˆ é™¤ã€‚ä¸ºæ­¤ï¼Œå¯ä»¥é‡‡å–é‡å çš„æ–¹æ³•ï¼Œé€šè¿‡ç²¾å¿ƒé€‰å–åˆé€‚çš„é‡å æ–¹å¼ï¼Œå°†å¤šä¸ªæ–‡ä»¶å¤´é‡å åœ¨ä¸€èµ·ï¼Œå¹¶ä¿è¯é‡å ä¹‹åï¼Œæ¯ä¸ªé‡å çš„ä½ç½®æœ€å¤šåªèƒ½å¯¹åº”ä¸€ä¸ªæœ‰ç”¨å­—æ®µã€‚è¿™æ ·å°±å¯ä»¥åœ¨ä¸ç ´åæ–‡ä»¶å¤´çš„å‰æä¸‹ï¼Œæœ‰æ•ˆåœ°å‡å°æ–‡ä»¶å¤§å°ã€‚**

è¦åˆ¤æ–­ä¸€ä¸ªå­—æ®µç©¶ç«Ÿæ˜¯æœ‰ç”¨å­—æ®µè¿˜æ˜¯æ— ç”¨å­—æ®µï¼Œå¯ä»¥é€ä¸ªä¿®æ”¹ `stretch.asm` ä¸­æ¯ä¸ªå­—æ®µçš„å€¼ï¼Œæ¯”å¦‚ä¿®æ”¹ä¸º 0ã€‚å¦‚æœä¿®æ”¹ä»¥åç¨‹åºå‡ºç°é—®é¢˜ï¼Œåˆ™è¯´æ˜è¯¥å­—æ®µæ˜¯æœ‰ç”¨å­—æ®µï¼Œå¦åˆ™æ˜¯æ— ç”¨å­—æ®µã€‚

åˆ¤æ–­å‡ºæœ‰ç”¨å­—æ®µå’Œæ— ç”¨å­—æ®µåï¼Œå¯ä»¥å°†æœ‰ç”¨å­—æ®µä¸æ— ç”¨å­—æ®µç›¸äº’é‡å ï¼Œä»è€Œå‡å°æ–‡ä»¶å¤§å°ã€‚ä¾‹å¦‚ï¼Œå°† PE header çš„èµ·å§‹ä½ç½®è®¾ç½®ä¸º DOS header çš„ `0x04` å¤„ï¼Œå¯ä»¥å°†ä¸¤ä¸ªæ–‡ä»¶å¤´çš„å­—æ®µé‡å ã€‚é‡å æ—¶å¯èƒ½å­˜åœ¨å¤šç§æ–¹æ³•ï¼Œæœ¬æ¬¡å®éªŒåªé€‰æ‹©å…¶ä¸­ä¸€ç§æ–¹æ³•ã€‚

å¦å¤–ï¼Œè™½ç„¶ Import table å’Œ DLLFuncEntry éƒ½æ˜¯æœ‰ç”¨å­—æ®µï¼Œä½†æ˜¯ä¸¤è€…å¯ä»¥é‡å ã€‚è¿™æ˜¯å› ä¸º Import table åªåœ¨ç¨‹åºåŠ è½½å‰ä½¿ç”¨ï¼ŒDLLFuncEntry åªåœ¨ç¨‹åºåŠ è½½åä½¿ç”¨ï¼Œæ‰€ä»¥å¹¶ä¸ä¼šäº§ç”Ÿå†²çªã€‚

é‡å åçš„ `stretch.asm` å¦‚ä¸‹ï¼š

```assembly
BITS 64

                            ; DOS Header
    dw 'MZ'                 ; e_magic
    dw 0                    ; [UNUSED] e_cblp
pe_hdr:                                                 ; PE Header
    dw 'PE'                 ; [UNUSED] c_cp             ; Signature
    dw 0                    ; [UNUSED] e_crlc           ; Signature (Cont)
                                                        ; Image File Header
    dw 0x8664               ; [UNUSED] e_cparhdr        ; Machine
code:
symbol:                                                                                     ; Symbol
    dw 0x01                 ; [UNUSED] e_minalloc       ; NumberOfSections                  ; [UNUSED] Function Order
    db 'MessageBoxW', 0                                                                     ; Function Name
    times 14-($-symbol) db 0; [UNUSED] e_maxalloc       ; [UNUSED] TimeDateStamp
                            ; [UNUSED] e_ss             ; [UNUSED] TimeDateStamp (Cont)
                            ; [UNUSED] e_sp             ; [UNUSED] PointerToSymbolTable
                            ; [UNUSED] e_csum           ; [UNUSED] PointerToSymbolTable (Cont)
                            ; [UNUSED] e_ip             ; [UNUSED] NumberOfSymbols
                            ; [UNUSED] e_cs             ; [UNUSED] NumberOfSymbols (Cont)
    dw opt_hdr_size         ; [UNUSED] e_lfarlc         ; SizeOfOptionalHeader
    dw 0x22                 ; [UNUSED] e_ovno           ; Characteristics
opt_hdr:                                                ; Optional Header, COFF Standard Fields
    dw 0x020b               ; [UNUSED] e_res            ; Magic (PE32+)
    db 0                    ; [UNUSED] e_res (Cont)     ; [UNUSED] MajorLinkerVersion
    db 0                    ; [UNUSED] e_res (Cont)     ; [UNUSED] MinorLinkerVersion
    dd code_size            ; [UNUSED] e_res (Cont)     ; SizeOfCode
    dw 0                    ; [UNUSED] e_oemid          ; [UNUSED] SizeOfInitializedData
    dw 0                    ; [UNUSED] e_oeminfo        ; [UNUSED] SizeOfInitializedData (Cont)
    dd 0                    ; [UNUSED] e_res2           ; [UNUSED] SizeOfUninitializedData
    dd entry                ; [UNUSED] e_res2 (Cont)    ; AddressOfEntryPoint
    dd code                 ; [UNUSED] e_res2 (Cont)    ; BaseOfCode
                                                        ; Optional Header, NT Additional Fields
    dq 0x000140000000       ; [UNUSED] e_res2 (Cont)    ; ImageBase
    dd pe_hdr               ; e_lfanew                  ; [MODIFIED] SectionAlignment (0x10 -> 0x04)
    dd 0x04                                             ; [MODIFIED] FileAlignment (0x10)
    dw 0x06                                             ; [UNUSED] MajorOperatingSystemVersion
    dw 0                                                ; [UNUSED] MinorOperatingSystemVersion
    dw 0                                                ; [UNUSED] MajorImageVersion
    dw 0                                                ; [UNUSED] MinorImageVersion
    dw 0x06                                             ; MajorSubsystemVersion
    dw 0                                                ; MinorSubsystemVersion
    dd 0                                                ; [UNUSED] Reserved1
    dd file_size                                        ; SizeOfImage
    dd hdr_size                                         ; SizeOfHeaders
    dd 0                                                ; [UNUSED] CheckSum
    dw 0x02                                             ; Subsystem (Windows GUI)
    dw 0x8160                                           ; DllCharacteristics
    dq 0x100000                                         ; SizeOfStackReserve
    dq 0x1000                                           ; SizeOfStackCommit
    dq 0x100000                                         ; SizeOfHeapReserve
dll_name:                                                                                   ; DLLName
    db 'USER32.dll', 0                                                                      ; DLLName
    times 12-($-dll_name) db 0                          ; [UNUSED] SizeOfHeapCommit
                                                        ; [UNUSED] LoaderFlags
    dd 0x02                                             ; [MODIFIED] NumberOfRvaAndSizes (0x10)

; Optional Header, Data Directories
    dd 0                    ; [UNUSED] Export, RVA
    dd 0                    ; [UNUSED] Export, Size
iatbl:                                                  ; Import Address Directory
    dd itbl                 ; Import, RVA               ; [USEDAFTERLOAD] DLLFuncEntry
    dd itbl_size            ; Import, Size              ; [USEDAFTERLOAD] DLLFuncEntry (Cont)
iatbl_size equ $-iatbl

opt_hdr_size equ $-opt_hdr

                            ; Section Table
    section_name db '.', 0  ; Name
    times 8-($-section_name) db 0
    dd sect_size            ; VirtualSize
    dd iatbl                ; VirtualAddress
    dd code_size            ; SizeOfRawData
    dd iatbl                ; PointerToRawData
content:                                                ; Strings
    db 0x41,0x00,0x42,0x00,0x43,0x00,0x44,0x00
    db 0x45,0x00,0x46,0x00,0x47,0x00,0,0
                            ; [UNUSED] PointerToRelocations
                            ; [UNUSED] PointerToLinenumbers
                            ; [UNUSED] NumberOfRelocations
                            ; [UNUSED] NumberOfLinenumbers
                            ; [UNUSED] Characteristics
hdr_size equ $-$$

title:
    db 0x3d,0xd8,0xaf,0xdc,0x20,0x00,0x54,0x00
    db 0x69,0x00,0x6e,0x00,0x79,0x00,0x50,0x00
    db 0x45,0x00,0x20,0x00,0x6f,0x00,0x6e,0x00
    db 0x20,0x00,0x57,0x00,0x69,0x00,0x6e,0x00
    db 0x64,0x00,0x6f,0x00,0x77,0x00,0x73,0x00
    db 0x20,0x00,0x31,0x00,0x30,0x00,0,0

; Entry
entry:
    mov r9d, 0x00240040     ; uType
    lea r8, [rel title]     ; lpCaption
    lea rdx, [rel content]  ; lpText
    xor ecx, ecx            ; hWnd
    jmp [rel iatbl]         ; MessageBoxW

itbl:                       ; Import Directory
    dq intbl                ; OriginalFirstThunk
    dd 0                    ; [UNUSED] TimeDateStamp
    dd dll_name             ; ForwarderChain
    dd iatbl                ; Name
intbl:                                                  ; Import Name Table
    dq symbol               ; [UNUSED] FirstThunk       ; Symbol
    dq 0                                                ; nullptr
itbl_size equ $-itbl

sect_size equ $-code
code_size equ $-code
file_size equ $-$$
```

ç¼–è¯‘ï¼š

```zsh
$ nasm -f bin -o stretch.exe -l stretch.lst stretch.asm
```

ç¼–è¯‘ç”Ÿæˆ `stretch.exe`ï¼Œå¯ä»¥æ­£å¸¸è¿è¡Œã€‚

**ç¬¬å…«æ­¥ï¼šPE æ–‡ä»¶ä¸­è¿˜å«æœ‰äº”æ¡æŒ‡ä»¤çš„æœºå™¨ç ï¼Œå¦‚æœå°†è¿™äº”æ¡æŒ‡ä»¤çš„æœºå™¨ç ä¹ŸæŒ‰ä¸Šä¸€æ­¥çš„æ–¹æ³•ä¸æ–‡ä»¶å¤´é‡å ï¼Œå°±å¯ä»¥è¿›ä¸€æ­¥å‡å°æ–‡ä»¶çš„å¤§å°ã€‚ä½†æ˜¯ï¼ŒæŒ‡ä»¤çš„æœºå™¨ç å¤ªé•¿ï¼Œæ²¡æœ‰åŠæ³•åšåˆ°ã€Œè§ç¼æ’é’ˆã€ï¼Œæ”¾å…¥æ–‡ä»¶å¤´æ— ç”¨å­—æ®µçš„ç©ºéš™ä¸­ã€‚ä¸ºæ­¤ï¼Œæƒ³åˆ°å¯ä»¥å°†äº”æ¡æŒ‡ä»¤æ‹†å¼€ï¼Œå¹¶åœ¨å‰å››æ¡æŒ‡ä»¤çš„åé¢åˆ†åˆ«åŠ ä¸€æ¡çŸ­è·³è½¬æŒ‡ä»¤è·³åˆ°ä¸‹ä¸€æ¡æŒ‡ä»¤çš„ä½ç½®ï¼Œè¿™æ ·æ¯æ¡æŒ‡ä»¤å°±å¯ä»¥ä½œä¸ºä¸€ä¸ªç‹¬ç«‹çš„éƒ¨åˆ†æ’å…¥åˆ°ä¸åŒçš„ç©ºéš™ä¸­äº†ã€‚ç„¶è€Œï¼Œå³ä½¿è¿™æ ·åšä»ç„¶æœ‰ä¸¤æ¡æŒ‡ä»¤å¤ªé•¿ï¼Œæ²¡æœ‰åŠæ³•æ’å…¥ç©ºéš™ã€‚è¿™æ—¶ï¼Œé€šè¿‡å¯¹ x64 æŒ‡ä»¤é›†çš„æ·±å…¥å­¦ä¹ å’Œå……åˆ†æŒæ¡ï¼Œæƒ³åˆ°åœ¨è¿™ä¸¤æ¡æŒ‡ä»¤ä»¥å¦ä¸€ä¸ªå¯„å­˜å™¨ä½œä¸ºåŸºå€æ—¶ï¼Œå¯ä»¥ä½¿å¯¹åº”çš„æœºå™¨ç æ›´çŸ­ï¼Œè€ŒæŒ‡ä»¤ç»“æœä¸å˜ã€‚è¿™æ ·æœºå™¨ç ä¹Ÿæ’å…¥åˆ°äº†æ–‡ä»¶å¤´æ— ç”¨å­—æ®µçš„ç©ºéš™ä¸­ï¼Œä»è€Œè¿›ä¸€æ­¥å‡å°äº†æ–‡ä»¶å¤§å°ã€‚**

åœ¨ä¸Šä¸€æ­¥éª¤ä¸­ï¼Œèƒ½é‡å çš„å­—æ®µéƒ½å·²ç»è¢«é‡å äº†ï¼Œè€ŒæŒ‡ä»¤çš„æœºå™¨ç è¿˜æ²¡æœ‰ä½œè¿‡å˜åŒ–ï¼š

```zsh
$ grep -A 5 entry: stretch.lst
    94                                  entry:
    95 000000F4 41B940002400                mov r9d, 0x00240040     ; uType
    96 000000FA 4C8D05C3FFFFFF              lea r8, [rel title]     ; lpCaption
    97 00000101 488D15ACFFFFFF              lea rdx, [rel content]  ; lpText
    98 00000108 31C9                        xor ecx, ecx            ; hWnd
    99 0000010A FF2584FFFFFF                jmp [rel iatbl]         ; MessageBoxW
```

å…¶ä¸­ï¼Œå‰å››æ¡æŒ‡ä»¤ç”¨äºå‡½æ•°è°ƒç”¨çš„å‚æ•°ä¼ é€’ã€‚æ ¹æ® [x64 calling convention](https://docs.microsoft.com/en-us/cpp/build/x64-calling-convention?view=vs-2019)ï¼Œåœ¨ Windows x64 ä¸­ï¼Œå‡½æ•°çš„å‰å››ä¸ªå‚æ•°åˆ†åˆ«ä½¿ç”¨ RCXã€RDXã€R8 å’Œ R9 ä¼ é€’ï¼Œåªæœ‰å½“å‚æ•°å¤§äºå››ä¸ªæ—¶æ‰ä½¿ç”¨å †æ ˆä¼ é€’ã€‚`MessageBoxW` çš„ `hWnd` å’Œ `uType` ä¸¤ä¸ªå‚æ•°çš„é•¿åº¦ä¸º 32 ä½è€Œé 64 ä½ï¼Œå› æ­¤è¦å°† RCX æ›¿æ¢ä¸º ECXï¼ŒR9 æ›¿æ¢ä¸º R9Dã€‚ç¬¬äº”æ¡æŒ‡ä»¤æ˜¯è·³è½¬æŒ‡ä»¤ã€‚ä¸ºäº†å‡å° PE æ–‡ä»¶çš„å¤§å°ï¼Œä¸å¿…å¤„ç† `MessageBoxW` å‡½æ•°çš„è¿”å›å€¼ï¼Œç›´æ¥ä½¿ç”¨ `jmp` æŒ‡ä»¤è·³è½¬åˆ°ç›®æ ‡å‡½æ•°ã€‚

è¿™ç¬¬äº”æ¡æŒ‡ä»¤çš„æœºå™¨ç ä½œä¸ºä¸€ä¸ªæ•´ä½“å…±å  28 å­—èŠ‚ï¼Œä½†ä¸Šä¸€æ­¥ç»è¿‡ä¸€ç•ªé‡å ï¼Œæœ€å¤§çš„æ— ç”¨å­—æ®µä¹Ÿåªæœ‰ 8 å­—èŠ‚ã€‚å› æ­¤ï¼Œæ²¡æœ‰åŠæ³•åšåˆ°ã€Œè§ç¼æ’é’ˆã€ï¼Œå°†æœºå™¨ç æ”¾å…¥æ–‡ä»¶å¤´æ— ç”¨å­—æ®µçš„ç©ºéš™ä¸­ã€‚è¿™æ—¶ï¼Œæƒ³åˆ°å¯ä»¥å°†æŒ‡ä»¤æ‹†å¼€ï¼Œç„¶ååœ¨å‰å››æ¡æŒ‡ä»¤çš„åé¢åˆ†åˆ«åŠ ä¸€æ¡çŸ­è·³è½¬æŒ‡ä»¤è·³åˆ°ä¸‹ä¸€æ¡æŒ‡ä»¤çš„ä½ç½®ã€‚ç”± [JMP â€” Jump](https://www.felixcloutier.com/x86/jmp) å¯çŸ¥ï¼ŒçŸ­è·³è½¬æŒ‡ä»¤çš„è·³è½¬èŒƒå›´ä¸º â€“128 è‡³ +127ï¼Œæœºå™¨ç åªå  2 ä¸ªå­—èŠ‚ï¼Œæ‰€ä»¥è¿™ç§åšæ³•æ˜¯å¯è¡Œçš„ã€‚

| æ±‡ç¼–æŒ‡ä»¤ | æœºå™¨ç é•¿åº¦ |
| :- | :- |
| `mov r9d, 0x00240040` + `jmp` | 8 |
| `lea r8, [rel title]` + `jmp` | 9 |
| `lea rdx, [rel content]` + `jmp` | 9 |
| `xor ecx, ecx` + `jmp` | 4 |
| `jmp [rel iatbl]` | 6 |

ä½†æ˜¯ï¼Œé€šè¿‡ä¸Šè¡¨å¯ä»¥çœ‹å‡ºï¼Œä¸¤æ¡ `lea` æŒ‡ä»¤çš„æœºå™¨ç ä»å  9 ä¸ªå­—èŠ‚ã€‚è€Œä¸Šé¢æåˆ°ï¼Œæœ€å¤§çš„æ— ç”¨å­—æ®µä¹Ÿåªæœ‰ 8 å­—èŠ‚ï¼Œæ‰€ä»¥å¯¹äºè¿™ä¸¤æ¡æŒ‡ä»¤ï¼Œä»ç„¶æ²¡åŠæ³•åšåˆ°ã€Œè§ç¼æ’é’ˆã€ã€‚

åœ¨ [x64dbg](https://x64dbg.com/) ä¸­è°ƒè¯•æ—¶å‘ç°ï¼Œå½“ç¨‹åºæ‰§è¡Œåˆ°ç”¨æˆ·ä»£ç çš„å…¥å£ç‚¹æ—¶ï¼ŒRDX å¯„å­˜å™¨çš„å€¼ä¼šè¢«è®¾ç½®ä¸ºå…¥å£åœ°å€ï¼š

![](images/rdx.png)

è¿™æ—¶ï¼Œé€šè¿‡å¯¹ x64 æŒ‡ä»¤é›†çš„æ·±å…¥å­¦ä¹ å’Œå……åˆ†æŒæ¡ï¼Œç«‹å³æ„è¯†åˆ°åœ¨ `lea` æŒ‡ä»¤ä¸­ï¼Œå¦‚æœä»¥ RDX å¯„å­˜å™¨ä½œä¸ºåŸºå€ï¼Œå¯ä»¥ä½¿å¯¹åº”çš„æœºå™¨ç æ›´çŸ­ã€‚RDX å¯„å­˜å™¨çš„å€¼ä¼šè¢«è®¾ç½®ä¸ºå…¥å£åœ°å€ï¼Œä¹Ÿå°±æ˜¯è¯´ RDX å¯„å­˜å™¨çš„å€¼ä¸æ˜¯éšæœºçš„ï¼Œä¹Ÿå°±å…·å¤‡äº†ä½œåŸºå€çš„æ¡ä»¶ã€‚

åœ¨åŸæ¥çš„ç¨‹åºä¸­ï¼Œä»¥ RIP å¯„å­˜å™¨ä½œä¸ºåŸºå€ï¼Œå¯¹åº”çš„æœºå™¨ç é•¿åº¦ä¸º 7ï¼š

| æ±‡ç¼–æŒ‡ä»¤ | æœºå™¨ç  | é•¿åº¦ |
| :- | :- | :- |
| `lea r8, [rip-0x4d]` | 0x4c8d05b3ffffff | 7 |
| `lea rdx, [rip-0x44]` | 0x488d15bcffffff | 7 |

è€Œä»¥ RDX å¯„å­˜å™¨ä½œä¸ºåŸºå€æ—¶ï¼Œå¯¹åº”çš„æœºå™¨ç é•¿åº¦ä¸º 4ï¼š

| æ±‡ç¼–æŒ‡ä»¤ | æœºå™¨ç  | é•¿åº¦ |
| :- | :- | :- |
| `lea r8, [rdx-0x4d]` | 0x4c8d42b3 | 4 |
| `lea rdx, [rdx-0x44]` | 0x488d52bc | 4 |

å› æ­¤ï¼Œå°†ä¸¤æ¡ `lea` æŒ‡ä»¤æ”¹ä¸ºä»¥ RDX å¯„å­˜å™¨ä½œä¸ºåŸºå€ã€‚ä¿®æ”¹åçš„æ±‡ç¼–æŒ‡ä»¤å¦‚ä¸‹ï¼š

| æ±‡ç¼–æŒ‡ä»¤ | æœºå™¨ç é•¿åº¦ |
| :- | :- |
| `mov r9d, 0x00240040` + `jmp` | 8 |
| `lea r8, [rdx+title-entry]` + `jmp` | 6 |
| `lea rdx, [rdx+content-entry]` + `jmp` | 6 |
| `xor ecx, ecx` + `jmp` | 4 |
| `jmp [rel iatbl]` | 6 |

è¿™æ ·å°±å¯ä»¥ä¸æ— ç”¨å­—æ®µé‡å äº†ã€‚

ä¿®æ”¹åçš„ `stretch.asm` å¦‚ä¸‹ï¼š

```assembly
BITS 64

                            ; DOS Header
    dw 'MZ'                 ; e_magic
    dw 0                    ; [UNUSED] e_cblp
pe_hdr:                                                 ; PE Header
    dw 'PE'                 ; [UNUSED] c_cp             ; Signature
    dw 0                    ; [UNUSED] e_crlc           ; Signature (Cont)
                                                        ; Image File Header
    dw 0x8664               ; [UNUSED] e_cparhdr        ; Machine
code:
symbol:                                                                                     ; Symbol
    dw 0x01                 ; [UNUSED] e_minalloc       ; NumberOfSections                  ; [UNUSED] Function Order
    db 'MessageBoxW', 0                                                                     ; Function Name
    times 14-($-symbol) db 0; [UNUSED] e_maxalloc       ; [UNUSED] TimeDateStamp
                            ; [UNUSED] e_ss             ; [UNUSED] TimeDateStamp (Cont)
                            ; [UNUSED] e_sp             ; [UNUSED] PointerToSymbolTable
                            ; [UNUSED] e_csum           ; [UNUSED] PointerToSymbolTable (Cont)
                            ; [UNUSED] e_ip             ; [UNUSED] NumberOfSymbols
                            ; [UNUSED] e_cs             ; [UNUSED] NumberOfSymbols (Cont)
    dw opt_hdr_size         ; [UNUSED] e_lfarlc         ; SizeOfOptionalHeader
    dw 0x22                 ; [UNUSED] e_ovno           ; Characteristics
opt_hdr:                                                ; Optional Header, COFF Standard Fields
    dw 0x020b               ; [UNUSED] e_res            ; Magic (PE32+)
    db 0                    ; [UNUSED] e_res (Cont)     ; [UNUSED] MajorLinkerVersion
    db 0                    ; [UNUSED] e_res (Cont)     ; [UNUSED] MinorLinkerVersion
    dd code_size            ; [UNUSED] e_res (Cont)     ; SizeOfCode
code_4:                                                                                     ; Code Fragment 4
    jmp [rel iatbl]                                                                         ; MessageBoxW
    times 8-($-code_4) db 0 ; [UNUSED] e_oemid          ; [UNUSED] SizeOfInitializedData
                            ; [UNUSED] e_oeminfo        ; [UNUSED] SizeOfInitializedData (Cont)
                            ; [UNUSED] e_res2           ; [UNUSED] SizeOfUninitializedData
    dd entry                ; [UNUSED] e_res2 (Cont)    ; AddressOfEntryPoint
    dd code                 ; [UNUSED] e_res2 (Cont)    ; BaseOfCode
                                                        ; Optional Header, NT Additional Fields
    dq 0x000140000000       ; [UNUSED] e_res2 (Cont)    ; ImageBase
    dd pe_hdr               ; e_lfanew                  ; [MODIFIED] SectionAlignment (0x10 -> 0x04)
    dd 0x04                                             ; [MODIFIED] FileAlignment (0x10)
code_3:                                                                                     ; Code Fragment 3
    lea rdx, [rdx+content-entry]                                                            ; lpText
    jmp code_4
    times 8-($-code_3) db 0                             ; [UNUSED] MajorOperatingSystemVersion
                                                        ; [UNUSED] MinorOperatingSystemVersion
                                                        ; [UNUSED] MajorImageVersion
                                                        ; [UNUSED] MinorImageVersion
    dw 0x06                                             ; MajorSubsystemVersion
    dw 0                                                ; MinorSubsystemVersion
    dd 0                                                ; [UNUSED] Reserved1
    dd file_size                                        ; SizeOfImage
    dd hdr_size                                         ; SizeOfHeaders
    dd 0                                                ; [UNUSED] CheckSum
    dw 0x02                                             ; Subsystem (Windows GUI)
    dw 0x8160                                           ; DllCharacteristics
    dq 0x100000                                         ; SizeOfStackReserve
    dq 0x1000                                           ; SizeOfStackCommit
    dq 0x100000                                         ; SizeOfHeapReserve
dll_name:                                                                                   ; DLLName
    db 'USER32.dll', 0                                                                      ; DLLName
    times 12-($-dll_name) db 0                          ; [UNUSED] SizeOfHeapCommit
                                                        ; [UNUSED] LoaderFlags
    dd 0x02                                             ; [MODIFIED] NumberOfRvaAndSizes (0x10)

                            ; Optional Header, Data Directories
code_2:                                                 ; Code Fragment 2
    mov r9d, 0x00240040                                 ; uType
    jmp code_3
    times 8-($-code_2) db 0 ; [UNUSED] Export, RVA
                            ; [UNUSED] Export, Size
iatbl:                                                  ; Import Address Directory
    dd itbl                 ; Import, RVA               ; [USEDAFTERLOAD] DLLFuncEntry
    dd itbl_size            ; Import, Size              ; [USEDAFTERLOAD] DLLFuncEntry (Cont)
iatbl_size equ $-iatbl

opt_hdr_size equ $-opt_hdr

                            ; Section Table
    section_name db '.', 0  ; Name
code_1:                                                 ; Code Fragment 1
    lea r8, [rdx+title-entry]                           ; lpCaption
    jmp code_2
    times 8-($-section_name) db 0
    dd sect_size            ; VirtualSize
    dd iatbl                ; VirtualAddress
    dd code_size            ; SizeOfRawData
    dd iatbl                ; PointerToRawData
content:                                                ; Strings
    db 0x41,0x00,0x42,0x00,0x43,0x00,0x44,0x00
    db 0x45,0x00,0x46,0x00,0x47,0x00,0,0
                            ; [UNUSED] PointerToRelocations
                            ; [UNUSED] PointerToLinenumbers
                            ; [UNUSED] NumberOfRelocations
                            ; [UNUSED] NumberOfLinenumbers
                            ; [UNUSED] Characteristics
hdr_size equ $-$$

title:
    db 0x3d,0xd8,0xaf,0xdc,0x20,0x00,0x54,0x00
    db 0x69,0x00,0x6e,0x00,0x79,0x00,0x50,0x00
    db 0x45,0x00,0x20,0x00,0x6f,0x00,0x6e,0x00
    db 0x20,0x00,0x57,0x00,0x69,0x00,0x6e,0x00
    db 0x64,0x00,0x6f,0x00,0x77,0x00,0x73,0x00
    db 0x20,0x00,0x31,0x00,0x30,0x00,0,0

itbl:                       ; Import Directory
    dq intbl                ; OriginalFirstThunk
entry:                                                  ; Code Fragment 0
    xor ecx, ecx                                        ; hWnd
    jmp code_1
    times 4-($-entry) db 0  ; [UNUSED] TimeDateStamp
    dd dll_name             ; ForwarderChain
    dd iatbl                ; Name
intbl:                                                  ; Import Name Table
    dq symbol               ; [UNUSED] FirstThunk       ; Symbol
itbl_size equ $-itbl
    dq 0                                                ; nullptr

sect_size equ $-code
code_size equ $-code
file_size equ $-$$
```

ç¼–è¯‘ï¼š

```zsh
$ nasm -f bin -o stretch.exe -l stretch.lst stretch.asm
```

ç¼–è¯‘ç”Ÿæˆ `stretch.exe`ï¼Œå¯ä»¥æ­£å¸¸è¿è¡Œã€‚

**ç¬¬ä¹æ­¥ï¼šåˆ é™¤æ–‡ä»¶æœ«å°¾çš„ 0ï¼Œå› ä¸ºç¨‹åºåŠ è½½æ—¶ä¼šåœ¨æœ«å°¾è‡ªåŠ¨å¡«å……é›¶ã€‚**

ç¨‹åºåŠ è½½æ—¶ä¼šåœ¨æœ«å°¾è‡ªåŠ¨å¡«å…… 0ï¼Œå› æ­¤æ–‡ä»¶æœ«å°¾çš„ 0 å¯ä»¥åˆ å»ã€‚

å°†ä¿®æ”¹åçš„ç»“æœä¿å­˜ä¸º `stretch.asm`ï¼š

```zsh
$ diff step8/stretch.asm step9/stretch.asm
113c113
<     dq symbol               ; [UNUSED] FirstThunk       ; Symbol
---
>     dd symbol
115d114
<     dq 0                                                ; nullptr
```

ç¼–è¯‘ï¼š

```zsh
$ nasm -f bin -o stretch.exe -l stretch.lst stretch.asm
```

ç¼–è¯‘ç”Ÿæˆ `stretch.exe`ï¼Œå¯ä»¥æ­£å¸¸è¿è¡Œï¼š

![](images/tiny1.exe.png)

```zsh
$ xxd stretch.exe
00000000: 4d5a 0000 5045 0000 6486 0100 4d65 7373  MZ..PE..d...Mess
00000010: 6167 6542 6f78 5700 8000 2200 0b02 0000  ageBoxW...".....
00000020: 0201 0000 ff25 6a00 0000 0000 fc00 0000  .....%j.........
00000030: 0a00 0000 0000 0040 0100 0000 0400 0000  .......@........
00000040: 0400 0000 488d 52b8 ebda 0000 0600 0000  ....H.R.........
00000050: 0000 0000 0c01 0000 c400 0000 0000 0000  ................
00000060: 0200 6081 0000 1000 0000 0000 0010 0000  ..`.............
00000070: 0000 0000 0000 1000 0000 0000 5553 4552  ............USER
00000080: 3332 2e64 6c6c 0000 0200 0000 41b9 4000  32.dll......A.@.
00000090: 2400 ebb0 f400 0000 1800 0000 2e00 4c8d  $.............L.
000000a0: 42c8 ebe8 0201 0000 9400 0000 0201 0000  B...............
000000b0: 9400 0000 4100 4200 4300 4400 4500 4600  ....A.B.C.D.E.F.
000000c0: 4700 0000 3dd8 afdc 2000 5400 6900 6e00  G...=... .T.i.n.
000000d0: 7900 5000 4500 2000 6f00 6e00 2000 5700  y.P.E. .o.n. .W.
000000e0: 6900 6e00 6400 6f00 7700 7300 2000 3100  i.n.d.o.w.s. .1.
000000f0: 3000 0000 0801 0000 0000 0000 31c9 eb9e  0...........1...
00000100: 7c00 0000 9400 0000 0a00 0000            |...........
```

## å‚è€ƒèµ„æ–™

é™¤æ–‡ä¸­å·²è¯´æ˜çš„å‚è€ƒèµ„æ–™å¤–ï¼Œæœ¬å®éªŒè¿˜å‚è€ƒäº†ä»¥ä¸‹èµ„æ–™ï¼š

1. [Tiny PE](http://www.phreedom.org/research/tinype/)
1. [Writing ultra-small Windows executables](https://keyj.emphy.de/win32-pe/)
